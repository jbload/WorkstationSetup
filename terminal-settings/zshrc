export HISTCONTROL=ignoreboth
export HISTSIZE=1000
export HISTFILESIZE=2000
export GIT_DUET_ROTATE_AUTHOR=1
export GIT_DUET_ALLOW_MULTIPLE_COMMITTERS=1


export git_push_during_sync=1
export git_repos=()
: '
This could be set explicitly like:
    export git_repos=(
        ~/workspace/repo1
        ~/workspace/repo2
        ~/workspace/repo3
    )

Or it you can use wildcards like:
    export git_repos=(~/workspace/client-*)
'
export WORKSPACE=~/workspace

if [ -f ~/.zshrc-pre-init ]; then
    . ~/.zshrc-pre-init
fi

setopt APPEND_HISTORY
autoload -U +X bashcompinit && bashcompinit
[ -x /usr/bin/lesspipe ] && eval "$(SHELL=/bin/sh lesspipe)"

# If you come from bash you might have to change your $PATH.
export PATH=$PATH:$HOME/bin
export PATH=$PATH:/usr/local/bin/
export PATH=$PATH:/usr/local
export PATH=$PATH:$HOME/.fastlane/bin
export PATH=$PATH:$HOME/.rbenv/bin
export PATH=$PATH:/usr/local/opt/mysql-client/bin

# Path to your oh-my-zsh installation.
export ZSH="$HOME/.oh-my-zsh"

# Set name of the theme to load --- if set to "random", it will
# load a random theme each time oh-my-zsh is loaded, in which case,
# to know which specific one was loaded, run: echo $RANDOM_THEME
# See https://github.com/robbyrussell/oh-my-zsh/wiki/Themes
ZSH_THEME="muse"

# Set list of themes to pick from when loading at random
# Setting this variable when ZSH_THEME=random will cause zsh to load
# a theme from this variable instead of looking in ~/.oh-my-zsh/themes/
# If set to an empty array, this variable will have no effect.
# ZSH_THEME_RANDOM_CANDIDATES=( "robbyrussell" "agnoster" )

# Uncomment the following line to use case-sensitive completion.
# CASE_SENSITIVE="true"

# Uncomment the following line to use hyphen-insensitive completion.
# Case-sensitive completion must be off. _ and - will be interchangeable.
# HYPHEN_INSENSITIVE="true"

# Uncomment the following line to disable bi-weekly auto-update checks.
# DISABLE_AUTO_UPDATE="true"

# Uncomment the following line to automatically update without prompting.
DISABLE_UPDATE_PROMPT="true"

# Uncomment the following line to change how often to auto-update (in days).
# export UPDATE_ZSH_DAYS=13

# Uncomment the following line if pasting URLs and other text is messed up.
# DISABLE_MAGIC_FUNCTIONS=true

# Uncomment the following line to disable colors in ls.
# DISABLE_LS_COLORS="true"

# Uncomment the following line to disable auto-setting terminal title.
# DISABLE_AUTO_TITLE="true"

# Uncomment the following line to enable command auto-correction.
# ENABLE_CORRECTION="true"

# Uncomment the following line to display red dots whilst waiting for completion.
# COMPLETION_WAITING_DOTS="true"

# Uncomment the following line if you want to disable marking untracked files
# under VCS as dirty. This makes repository status check for large repositories
# much, much faster.
# DISABLE_UNTRACKED_FILES_DIRTY="true"

# Uncomment the following line if you want to change the command execution time
# stamp shown in the history command output.
# You can set one of the optional three formats:
# "mm/dd/yyyy"|"dd.mm.yyyy"|"yyyy-mm-dd"
# or set a custom format using the strftime function format specifications,
# see 'man strftime' for details.
# HIST_STAMPS="mm/dd/yyyy"

# Would you like to use another custom folder than $ZSH/custom?
# ZSH_CUSTOM=/path/to/new-custom-folder

# Which plugins would you like to load?
# Standard plugins can be found in ~/.oh-my-zsh/plugins/*
# Custom plugins may be added to ~/.oh-my-zsh/custom/plugins/
# Example format: plugins=(rails git textmate ruby lighthouse)
# Add wisely, as too many plugins slow down shell startup.
# https://github.com/ohmyzsh/ohmyzsh/wiki/Plugins
plugins=(alias-finder brew git)

if [ -f ~/.oh-my-zsh-plugins ]; then
    . ~/.oh-my-zsh-plugins
fi

. $ZSH/oh-my-zsh.sh

# User configuration
if type microk8s > /dev/null; then
    alias mks='microk8s'
    alias kubectl='microk8s kubectl'
fi

if type ng > /dev/null; then
    source <(ng completion script)
fi

if type kubectl > /dev/null; then
    alias k=kubectl 
    alias kctx=kubectx
    source <(kubectl completion zsh)
fi

if type terraform > /dev/null; then
    alias tf=terraform
    terraform_path=$(which terraform)
    complete -o nospace -C $terraform_path terraform tf
    unset terraform_path
fi

if [ -f $HOME/.kube/config ]; then
    export KUBECONFIG=$KUBECONFIG:$HOME/.kube/config 
fi

if type subl > /dev/null; then
    editor_path=$(which subl)
    alias edit='subl'    
elif type code > /dev/null; then
    editor_path=$(which code)        
    alias edit='code'
elif type code-insiders > /dev/null; then
    editor_path=$(which code-insiders)        
    alias edit='code-insiders'    
else
    alias edit='vi'
fi

if type vim > /dev/null; then
    export KUBE_EDITOR=vim
else
    export KUBE_EDITOR=vi
fi

if [ -f $editor_path ]; then
    export EDITOR=$editor_path 
fi

unset editor_path

export BLOCKSIZE=1k
export NODE_OPTIONS=--max_old_space_size=8192
export PIPENV_VENV_IN_PROJECT=1
export ASPNETCORE_ENVIRONMENT=Development

# You may need to manually set your language environment
export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8

# Set personal aliases, overriding those provided by oh-my-zsh libs,
# plugins, and themes. Aliases can be placed here, though oh-my-zsh
# users are encouraged to define aliases within the ZSH_CUSTOM folder.
# For a full list of active aliases, run `alias`.
alias ...='cd ../../'                       # Go back 2 directory levels
alias ..='cd ../'                           # Go back 1 directory level
alias .3='cd ../../../'                     # Go back 3 directory levels
alias .4='cd ../../../../'                  # Go back 4 directory levels
alias .5='cd ../../../../../'               # Go back 5 directory levels
alias .6='cd ../../../../../../'            # Go back 6 directory levels
alias cd..='cd ../'                         # Go back 1 directory level (for fast typers)
alias cic='set completion-ignore-case On'   # cic:          Make tab-completion case-insensitive
alias cl='clear'                            # cl:           Clear terminal display
alias cls='clear'                           # cls:          Clear terminal display
alias cp='cp -iv'                           # Preferred 'cp' implementation
alias DT='tee ~/Desktop/terminalOut.txt'    # DT:           Pipe content to file on MacOS Desktop
alias editbashrc='edit ~/.bashrc'
alias editgitconfig='edit ~/.gitconfig'
alias editzshrc='edit ~/.zshrc'
alias f='open -a Finder ./'                 # f:            Opens current directory in MacOS Finder
alias fix_stty='stty sane'                  # fix_stty:     Restore terminal settings when screwed up
alias flushDNS='dscacheutil -flushcache'            # flushDNS:     Flush out the DNS Cache
alias ipInfo0='ipconfig getpacket en0'              # ipInfo0:      Get info on connections for en0
alias ipInfo1='ipconfig getpacket en1'              # ipInfo1:      Get info on connections for en1
alias less='less -FSRXc'                    # Preferred 'less' implementation
alias ll='ls -FGlAhp'                       # Preferred 'ls' implementation
alias lsock='sudo /usr/sbin/lsof -i -P'             # lsock:        Display open sockets
alias lsockT='sudo /usr/sbin/lsof -nP | grep TCP'   # lsockT:       Display only open TCP sockets
alias lsockU='sudo /usr/sbin/lsof -nP | grep UDP'   # lsockU:       Display only open UDP sockets
alias mkdir='mkdir -pv'                     # Preferred 'mkdir' implementation
alias mv='mv -iv'                           # Preferred 'mv' implementation
alias myip='curl https://dynamicdns.park-your-domain.com/getip'                    # myip:         Public facing IP Address
alias netCons='lsof -i'                             # netCons:      Show all open TCP/IP sockets
alias openPorts='sudo lsof -i | grep LISTEN'        # openPorts:    All listening connections
alias path='echo -e ${PATH//:/\\n}'         # path:         Echo all executable Paths
alias show_options='shopt'                  # Show_options: display bash options settings
alias showBlocked='sudo ipfw list'                  # showBlocked:  All ipfw rules inc/ blocked IPs
alias ws='cd $WORKSPACE'
alias xcdd='rm -rf ~/Library/Developer/Xcode/DerivedData'
alias zshrc='. ~/.zshrc'
alias ~="cd ~"                              # ~:            Go Home
alias yui="yarn upgrade-interactive"

cd () { builtin cd "$@"; ll; }              # Always list directory contents upon 'cd'
mcd () { mkdir -p "$1" && cd "$1"; }        # mcd:          Makes new Dir and jumps inside
ql () { qlmanage -p "$*" >& /dev/null; }    # ql:           Opens any file in MacOS Quicklook Preview
trash () { command mv "$@" ~/.Trash ; }     # trash:        Moves a file to the MacOS trash
zipf () { zip -r "$1".zip "$1" ; }          # zipf:         To create a ZIP archive of a folder

ii () {
    echo -e "\nYou are logged on ${RED}$HOST"
    echo -e "\nAdditionnal information:$NC " ; uname -a
    echo -e "\n${RED}Users logged on:$NC " ; w -h
    echo -e "\n${RED}Current date :$NC " ; date
    echo -e "\n${RED}Machine stats :$NC " ; uptime
    echo -e "\n${RED}Current network location :$NC " ; scselect
    echo -e "\n${RED}Public facing IP Address :$NC " ;myip
    #echo -e "\n${RED}DNS Configuration:$NC " ; scutil --dns
    echo
}

brewup () {
    echo Updating Homebrew...
    brew update

    echo Updating Homebrew Formulae...
    brew upgrade --formula

    echo Updating Homebrew Casks...
    brew upgrade --cask

    echo Cleaning Up Homebrew...
    brew cleanup

    echo Done!
}

gitsyncall () {
    local dirty_repos=()

    for repo in $git_repos
    do
        pushd $repo &> /dev/null

        if [ -n "$(git status --porcelain)" ]; then            
            dirty_repos+=($repo)
        else
            echo "********************************************************************************"
            echo "Synching git repo: $repo..."
            git up

            if [ $git_push_during_sync -eq 1 ]; then
                git pushf
            fi

            git com
            git up
            
            if [ $git_push_during_sync -eq 1 ]; then
                git pushf
            fi

            git co -
        fi
        
        popd &> /dev/null
    done

    if [ ${#dirty_repos[@]} -gt 0 ]; then
        echo "\nThe following repos were skipped because they had pending changes:"

        for dirty_repo in $dirty_repos            
        do
            echo "   $dirty_repo"
        done
    fi
}

gitstall () {
    local clean_repos=()

    for repo in $git_repos
    do
        pushd $repo &> /dev/null

        if [ -n "$(git status --porcelain)" ]; then   
            echo "********************************************************************************"
            echo "git status: $repo..."
            git st                     
        else
            clean_repos+=($repo)
        fi

        popd &> /dev/null
    done

    if [ ${#clean_repos[@]} -gt 0 ]; then
        echo "\nThe following repos were skipped because they had no pending changes:"

        for clean_repo in $clean_repos            
        do
            echo "   $clean_repo"
        done
    fi
}

gitgcall () {
    for repo in $git_repos
    do
        pushd $repo &> /dev/null

        echo "********************************************************************************"
        echo "Performing maintenance on git repo: $repo..."
        gitgc

        popd &> /dev/null
    done
}

gitgc () {
    git maintenance run --task gc
}

gitpruneall () {
    for repo in $git_repos
    do
        pushd $repo &> /dev/null

        echo "********************************************************************************"
        echo "Looking for local branches to delete from git repo: $repo..."
        gitprune

        popd &> /dev/null
    done
}

gitprune () {
    local branch_names=()

    git branch | grep -v '^*\|master\|main' | while read -r branch
    do
        branch_names+=($branch)
    done

    if [ ${#branch_names[@]} -gt 0 ]; then
        echo "\nLocal branches:"
        printf '%s\n   ' "${branch_names[@]}"
        echo ""

        if read -q "choice?Are you sure you want to remove these branches? (y/n): "; then
            for branch in $branch_names
            do
                git branch -D $branch
            done       
        else
            echo "\nNo branches were deleted."
        fi
    else
        echo "There are no local branches to delete."
    fi
}

rmdss() {
    sudo find . -name .DS_Store
    echo "\n"

    if read -q "choice?Are you sure you want to remove these folders? (y/n): "; then
        sudo find . -name .DS_Store -delete
        killall Finder    
    else
        echo "No folders were deleted."
    fi
}

remind() {
    echo "git:"
    echo "- Delete remote branch: git push origin --delete branch_name"
    echo "- Stop tracking a file: git update-index --assume-unchanged [<file>...]"
    echo "- Fix 'refs/remotes/origin/HEAD is not a symbolic ref' error when using git com alias: git remote set-head origin master"
    echo
    echo 'Docker:'
    echo "- Remove all Docker images: docker rmi \$(docker images -q) --force"
    echo
    echo "MacOS:"
    echo "- Capture screenshot of entire screen: Command + Shift + 3"
    echo "- Capture screenshot of selectable portion of screen: Command + Shift + 4"
    echo "- Capture screenshot with options: Command + Shift + 5"
}

# Setup GIT prompt (based on https://joshdick.net/2017/06/08/my_git_prompt_for_zsh_revisited.html)
setopt prompt_subst
autoload -U colors && colors # Enable colors in prompt

# Echoes a username/host string when connected over SSH (empty otherwise)
ssh_info() {
  [[ "$SSH_CONNECTION" != '' ]] && echo '%(!.%{$fg[red]%}.%{$fg[yellow]%})%n%{$reset_color%}@%{$fg[green]%}%m%{$reset_color%}:' || echo ''
}

# Echoes information about Git repository status when inside a Git repository
git_info() {

  # Exit if not inside a Git repository
  ! git rev-parse --is-inside-work-tree > /dev/null 2>&1 && return

  # Git branch/tag, or name-rev if on detached head
  local GIT_LOCATION=${$(git symbolic-ref -q HEAD || git name-rev --name-only --no-undefined --always HEAD)#(refs/heads/|tags/)}

  local AHEAD="%{$fg[red]%}⇡NUM%{$reset_color%}"
  local BEHIND="%{$fg[cyan]%}⇣NUM%{$reset_color%}"
  local MERGING="%{$fg[magenta]%}⚡︎%{$reset_color%}"
  local UNTRACKED="%{$fg[red]%}●%{$reset_color%}"
  local MODIFIED="%{$fg[yellow]%}●%{$reset_color%}"
  local STAGED="%{$fg[green]%}●%{$reset_color%}"

  local -a DIVERGENCES
  local -a FLAGS

  local NUM_AHEAD="$(git log --oneline @{u}.. 2> /dev/null | wc -l | tr -d ' ')"
  if [ "$NUM_AHEAD" -gt 0 ]; then
    DIVERGENCES+=( "${AHEAD//NUM/$NUM_AHEAD}" )
  fi

  local NUM_BEHIND="$(git log --oneline ..@{u} 2> /dev/null | wc -l | tr -d ' ')"
  if [ "$NUM_BEHIND" -gt 0 ]; then
    DIVERGENCES+=( "${BEHIND//NUM/$NUM_BEHIND}" )
  fi

  local GIT_DIR="$(git rev-parse --git-dir 2> /dev/null)"
  if [ -n $GIT_DIR ] && test -r $GIT_DIR/MERGE_HEAD; then
    FLAGS+=( "$MERGING" )
  fi

  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    FLAGS+=( "$UNTRACKED" )
  fi

  if ! git diff --quiet 2> /dev/null; then
    FLAGS+=( "$MODIFIED" )
  fi

  if ! git diff --cached --quiet 2> /dev/null; then
    FLAGS+=( "$STAGED" )
  fi

  local -a GIT_INFO
  GIT_INFO+=( "\033[38;5;15m±" )
  [ -n "$GIT_STATUS" ] && GIT_INFO+=( "$GIT_STATUS" )
  [[ ${#DIVERGENCES[@]} -ne 0 ]] && GIT_INFO+=( "${(j::)DIVERGENCES}" )
  [[ ${#FLAGS[@]} -ne 0 ]] && GIT_INFO+=( "${(j::)FLAGS}" )
  GIT_INFO+=( "\033[38;5;15m$GIT_LOCATION%{$reset_color%}" )
  echo "${(j: :)GIT_INFO}"

}

# Use $ as the non-root prompt character; # for root
# Change the prompt character color if the last command had a nonzero exit code
PS1='
$(ssh_info)%{$fg[green]%}%~%u $(git_info)
%(?.%{$fg[blue]%}.%{$fg[red]%})%(!.#.$)%{$reset_color%} '

if [[ -v $GO_TO_WORKSPACE_ON_STARTUP ]]; then
    builtin cd $WORKSPACE
fi

if [ -f ~/.zshrc-post-init ]; then
    . ~/.zshrc-post-init
fi

if type pyenv > /dev/null; then
  eval "$(pyenv init -)"
fi

if type rbenv > /dev/null; then
    eval "$(rbenv init -)"
fi

if type direnv > /dev/null; then
    eval "$(direnv hook zsh)"
fi

if [[ -v $CLEAR_SCREEN_ON_STARTUP ]]; then
    clear
fi

date